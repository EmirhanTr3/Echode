plugins {
    kotlin("jvm") version "2.3.20-Beta2"
    id("com.gradleup.shadow") version "8.3.0"
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("de.eldoria.plugin-yml.paper") version "0.7.1"
}

group = "cat.emir"
version = "1.0.0"

repositories {
    mavenCentral()
    maven("https://repo.papermc.io/repository/maven-public/")
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")
    paperLibrary("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("party.iroiro.luajava:luajava:4.1.0")
    implementation("party.iroiro.luajava:lua53:4.1.0")
    runtimeOnly("party.iroiro.luajava:lua53-platform:4.1.0:natives-desktop")
}

tasks {
    runServer {
        minecraftVersion("1.21.6")
    }

    jar.get().enabled = false

    build {
        dependsOn("shadowJar")
    }

    shadowJar {
        archiveClassifier = ""
    }
}

paper {
    authors = listOf("EmirhanTr3")
    description = "Make your server easily with simple syntax."
    website = "https://github.com/EmirhanTr3/Echode"
    main = "cat.emir.echode.Echode"
    loader = "cat.emir.echode.load.LibraryLoader"
    apiVersion = "1.21.10"

    // Keep this on!
    generateLibrariesJson = true

    serverDependencies {
    }
}

kotlin {
    jvmToolchain(21)
}